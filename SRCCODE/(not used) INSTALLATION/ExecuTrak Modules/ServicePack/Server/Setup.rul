//***************************************************************************
//
// Installation script for the Factor Service Pack release
//
//
//***************************************************************************

// includes

#include "winapi.h"

// constants
#define PRODUCT_VERSION         "3.09.02"  //CHANGE THE VESION BEFORE BUILDING DISK

#define SIZE_REQ_SERVER         4

#define PRODUCT_NAME            "Service Pack"
#define MODULE_NAME             "Service Pack"
#define APP_KEY_FILE            "FACTMENU.EXE"
#define KEY_DEFAULT             "SP"
#define KEY_CLIENT_PATH         "SP Client Path"
#define KEY_SERVER_PATH         "SP Server Path"
#define KEY_REMOTE              "SPRemote"
#define WELCOME_TITLE           "ExecuTrak Service Pack Installation"

#define COMPANY_NAME		"FACTOR"
#define APP_BASE_PATH		"Factor"
#define PRODUCT_SUBDIR		"ExecTrak"

#define PRODUCT_KEY             "FACTMENU.EXE"

#define DEVELOP			FALSE

// global variables

declare

#include "sddialog.h"

        //log setup variables and write log function prototype
	NUMBER nvFileOut;
	STRING INSTALL_OPTION,INSTALL_LINE;
	STRING INSWARN,LOGMSG,RESULTLOG;

	prototype LOGWRITE(STRING);

        NUMBER nReturn, nTemp;

        STRING KEY_ROOT, KEY_PRODUCT, KEY_VERSION;
        STRING szBaseDirectory, szServerDirectory, szDestMsg;
        STRING szMsg, szNumber, szTemp, szFolder, szDisk;
        STRING szSCIcon, szSCExe, szSCWork, szAppDir;
        STRING szOldRunValue, szNewRunValue, szComponents      
        STRING szDBFrom, szDBTo, szForceInstall;

        BOOL bRunStandAlone, bTemp1, bTemp2, bZFailure;;

        LIST    listInfo;

        // function prototypes
        prototype BaseInstalled();
        prototype CheckDiskSpace(number);

	// new variables and function definition
	NUMBER nvClientOptions, nvClientCount, nvMByteRequired;
	BOOL bServicePack;
	LIST lstClientID, lstClientName;

        	prototype fnFindFile(STRING, STRING);
	prototype UpdateServicePack(STRING, STRING);
	prototype DecompressZfile(STRING, STRING, NUMBER);
	prototype DecompressServicePack(STRING, LIST);
	prototype DeleteServiceZDirectory(LIST);
	prototype CopyServicePackFile(LIST, STRING);
	prototype CheckCopyFiles(LIST, STRING, STRING, BOOL, BOOL);


// the script starts here
program

StartHere:

        //set installation header
        if (SetFont(FONT_TITLE, 0, "Arail") < 0) then
                szMsg = "Installing " + PRODUCT_NAME;
                if (CMDLINE = "-A" || CMDLINE = "-a") then
                        szMsg = szMsg + "\nForced Installation";
                endif;
                szMsg = szMsg + "\nVersion " + PRODUCT_VERSION;
                SetTitle(szMsg , 18, WHITE );
        else
                szMsg = "Installing " + "ExecuTrak" + "™ " + MODULE_NAME;
                if (CMDLINE = "-A" || CMDLINE = "-a") then
                        szMsg = szMsg + "\nForced Installation";
                endif;
                szMsg = szMsg + "\nVersion " + PRODUCT_VERSION;
                SetTitle(szMsg, 18, WHITE );
 
        endif;

// ************************* Check for user defined command line
	INSTALL_OPTION=PRODUCT_NAME + " " + PRODUCT_VERSION;        
	INSTALL_LINE=CMDLINE;

        if( CreateFile( nvFileOut, WINSYSDIR, KEY_DEFAULT + "SETUP.TXT" ) < 0 ) then

    		MessageBeep( 0 );
                MessageBox( KEY_DEFAULT + "SETUP.TXT could not be opened", SEVERE );
            abort;
	else
		LOGMSG=INSTALL_OPTION + " SETUP PARAM= " + INSTALL_LINE;
		LOGWRITE(LOGMSG);
			
	endif;   

        if (CMDLINE = "-A" || CMDLINE = "-a") then
        	INSTALL_OPTION=INSTALL_OPTION + "\n      FORCED INSTALLATION";
		MessageBox("WARNING!  You are about to perform a " + "FORCED INSTALLATION of\n\n" + PRODUCT_NAME + " " + PRODUCT_VERSION
                        + "\n\nThis will OVERWRITE ALL FILES associated with this program,\nand may effect the operation of other programs that use the same files.\n\nPress 'Cancel' on the next screen and \n'Exit Setup' if you do not wish to do this.", WARNING);
                szForceInstall = "1 ";
        else
                szForceInstall = "0 ";
	endif;
// ************************* 

	// set registry keys
	KEY_ROOT = "SOFTWARE\\" + COMPANY_NAME;
	KEY_PRODUCT = KEY_ROOT + "\\" + PRODUCT_NAME;
	KEY_VERSION = KEY_PRODUCT + "\\" + PRODUCT_VERSION;
	
	RegDBSetDefaultRoot(HKEY_LOCAL_MACHINE);

	// set title for all dialog boxes
	SetDialogTitle(DLG_ASK_OPTIONS, WELCOME_TITLE);
	SetDialogTitle(DLG_ASK_TEXT, WELCOME_TITLE);
	SetDialogTitle(DLG_ASK_YESNO, WELCOME_TITLE);
	SetDialogTitle(DLG_MSG_INFORMATION, WELCOME_TITLE);
	SetDialogTitle(DLG_MSG_SEVERE, WELCOME_TITLE);
	SetDialogTitle(DLG_MSG_WARNING, WELCOME_TITLE);
	SetDialogTitle(DLG_USER_CAPTION, WELCOME_TITLE);

	//**************************************************
	//
	//		         MAIN INSTALLATION
	//
	//**************************************************

	// set some stuff
	Enable(INDVFILESTATUS);

	// set installation information
	InstallationInfo(COMPANY_NAME, PRODUCT_NAME, PRODUCT_VERSION, PRODUCT_KEY);


WelcomeScreen:

	// welcome message and get desired target path
	Disable(BACKBUTTON);
	Welcome(WELCOME_TITLE, 0);

        Enable(BACKBUTTON);


InstallationType:

	// is this a stand-alone installation?
	szComponents = "PC Server Installation";
	ComponentAddItem(szComponents, "PC Server Installation", 1024, TRUE);
	ComponentAddItem(szComponents, "Stand-alone PC Installation", 2000, FALSE);
	
	szTemp = "";
        if (SdAskOptions("Select Installation Type", "Choose the installation type by clicking " +
                 "the circle next to the appropriate option.",
                 "Once you have selected the installation type, click the Next " +
                 "button to continue.", szTemp, szComponents, EXCLUSIVE) = BACK) then

           goto WelcomeScreen;

        endif;

	if (ComponentIsItemSelected(szComponents,"PC Server Installation")) then
                bRunStandAlone = FALSE;
	else
                bRunStandAlone = TRUE;
	endif;

	NumToStr(szNumber, SIZE_REQ_SERVER * 1024);

        if (bRunStandAlone) then
                szBaseDirectory = TARGETDISK ^ APP_BASE_PATH ^ PRODUCT_SUBDIR;
        else
                szBaseDirectory = "J:" ^ APP_BASE_PATH ^ PRODUCT_SUBDIR;
        endif;


// get target path for local installation
GetTargetPath:
        szDestMsg = "Setup will install " + PRODUCT_NAME + " server files in the Destination Directory.\n\n";
        szDestMsg = szDestMsg + "To install to this directory, click Next.\n\n";
        szDestMsg = szDestMsg + "To install to a different directory, click Browse and select another ";
        szDestMsg = szDestMsg + "directory.\n\n";
        szDestMsg = szDestMsg + "You can choose not to install " + PRODUCT_NAME + " server files by clicking Cancel to";
	szDestMsg = szDestMsg + " exit Setup.\n\n";
	szDestMsg = szDestMsg + PRODUCT_NAME + " requires " + szNumber + " Kbytes of free disk space.\n";
    
        if (AskDestPath("", szDestMsg, szBaseDirectory, 0) = BACK) then

                goto InstallationType;

        endif;

	// check available disk space
	if (CheckDiskSpace(SIZE_REQ_SERVER * 1024 * 1024) = FALSE) then
		goto GetTargetPath;
	endif;

        ParsePath(szDisk, szBaseDirectory, DISK);
        szBaseDirectory = szDisk ^ APP_BASE_PATH;


CheckBaseInstalled:
        if (!BaseInstalled()) then
             szMsg = "Please install the ExecuTrak Base System BEFORE " +
                 "installing this module.\n\nSetup will be terminated.";
             MessageBox(szMsg,SEVERE);
             abort;
        endif;


ConfirmCopy:
        // Show SdStartCopy dialog to confirm file transfer operation.
        listInfo = ListCreate( STRINGLIST );

        //Construct the Info List
        ListAddString(listInfo, WELCOME_TITLE + ":", AFTER);
        if (bRunStandAlone) then
                ListAddString(listInfo, "        Stand-alone PC Installation", AFTER);
	else
                ListAddString(listInfo, "        PC Server Installation", AFTER);
	endif;
        ListAddString(listInfo, "", AFTER);
        ListAddString(listInfo, "installation Type:", AFTER);
        if (szForceInstall = "1 ") then
                ListAddString(listInfo, "        FORCE INSTALLATION", AFTER);
        else
                ListAddString(listInfo, "        NORMAL INSTALLATION", AFTER);
        endif;
        ListAddString(listInfo, "", AFTER);
        ListAddString(listInfo, PRODUCT_NAME + " program directory:", AFTER);
        szTemp = szBaseDirectory ^ PRODUCT_SUBDIR;
        ListAddString(listInfo, "        " + szTemp, AFTER);
        ListAddString(listInfo, "", AFTER);
        ListAddString(listInfo, PRODUCT_NAME + " Program Folder:", AFTER);
        if (bRunStandAlone) then
                ListAddString(listInfo, "        Program\\Factor" + "\\ExecuTak", AFTER);
        else
                ListAddString(listInfo, "        None", AFTER);
        endif;

        szMsg = "Setup has enough information to start copying " + PRODUCT_NAME + " files.  " +
                "If you want to review or change any settings, click Back.  If you are " +
                "satisfied with the settings, click Next to begin copying files.";
        if ( SdStartCopy( "Confirm Current Settings", szMsg, listInfo ) = BACK ) then
           ListDestroy( listInfo );
           goto GetTargetPath;
        endif;

        ListDestroy( listInfo );


StartCopy:

        	LOGMSG="INSTALLING IN : " + szBaseDirectory ^ PRODUCT_SUBDIR;
	LOGWRITE(LOGMSG);

	Disable(HOURGLASS);
	SdShowMsg("", FALSE);

	SetStatusWindow(0, "Copying " + PRODUCT_NAME + " server files...");
	Enable(STATUS);

        	if (bRunStandAlone) then
                	LOGMSG="Stand-alone PC Installation";
	else
                	LOGMSG="PC Server Installation";
	endif;
	LOGWRITE(LOGMSG);

        	FileSetBeginDefine("All Files");
	StatusUpdate(ON, 90);

// ************************* if command line =-a then FORCED INSTALLATION of ALL FILES

	if bRunStandAlone then
		TARGETDIR = WINDIR ^ "TEMP";
	else
		TARGETDIR = szBaseDirectory ^ "Clients" ^ PRODUCT_SUBDIR;
	endif;

        LOGMSG="START FORCED COPY OF " + KEY_DEFAULT + "client FILES IN " + TARGETDIR;
        LOGWRITE(LOGMSG);

        nReturn = CompressGet(KEY_DEFAULT + "client.z", "*.*", COMP_NORMAL);
        if (nReturn < 0) then
                MessageBox(KEY_DEFAULT + "CLIENT.Z could not be decompressed", SEVERE);
                goto DecompressError;
	endif;

        LOGMSG="END FORCED COPY OF " + KEY_DEFAULT + "client.z FILES IN " + TARGETDIR;
	LOGWRITE(LOGMSG);

        FileSetEndDefine("All Files");

        nReturn = FileSetPerformEz("All Files", 0);

        if (nReturn != FS_DONE) then
                LOGMSG="ERRORFILENAME=" + ERRORFILENAME;
                LOGWRITE(LOGMSG);
        endif;

        switch(nReturn)
                case FS_CREATEDIR:
                         MessageBox("Failed to Create target directory", SEVERE);
                         goto ZFAILURE;
                 case FS_PACKAGING:
                         MessageBox("Unable to find file in package list", SEVERE);
                         goto ZFAILURE;
                 case FS_FILENOTINLIB:
                         MessageBox("Unable to find target file in compressed lib", INFORMATION);
                         goto ZFAILURE;
                 case FS_DONE:
                         // do nothing
		 default:
                         MessageBox("General file copy failure", SEVERE);
                         goto ZFAILURE;
        endswitch;

   
        StatusUpdate(ON, 93);

DecompressError:
        if (nReturn < 0) then 
                LOGMSG="ERRORFILENAME=" + ERRORFILENAME;
                LOGWRITE(LOGMSG);

                switch(nReturn)
                        case COMP_DONE:
                                MessageBox("Indicates the function successfully decompressed the files from the library.", SEVERE);
                        case COMP_ERR_CREATEDIR:
                                MessageBox("A target directory could not be created.  Make sure that the path in the TARGETDIR system variable is syntactically correct and you have access rights to the target drive.", SEVERE);
                        case COMP_ERR_FILENOTINLIB:
                                MessageBox("The specified target file does not exist in the specified compressed library.  Use the InstallShield Data Compression Program (ICOMP.EXE at the DOS system prompt) with the -L (listing) option to view the contents of the compressed library and determine if the file specified is located in the library.", SEVERE);
                        case COMP_ERR_INCOMPATIBLE:
                                MessageBox("The library file is not compatible with InstallShield's compression format.", SEVERE);
                        case COMP_ERR_MEMORY:
                                MessageBox("The function cannot allocate the memory needed to complete the process.", SEVERE);
                        case COMP_ERR_NODISKSPACE:
                                MessageBox("The function cannot locate enough free space on the target disk.", SEVERE);
                        case COMP_ERR_OPENINPUT:
                                MessageBox("The function cannot create input files in the target directory.", SEVERE);
                        case COMP_ERR_OPENOUTPUT:
                                MessageBox("The function cannot find or open the output file from the source directory.", SEVERE);
                        case COMP_ERR_OPTIONS:
                                MessageBox("The specified nOptions is invalid.", SEVERE);
                        case COMP_ERR_TARGETREADONLY:
                                MessageBox("The file in TARGETDIR is read-only.", SEVERE);
                        default:
                                MessageBox("General decompression failure", SEVERE);
                endswitch;
                goto ZFAILURE;
        endif;

DecompressSuccessful:
        SetStatusWindow(95, "Running Service Pack Update...");

	if bRunStandAlone then
		szTemp = WINDIR ^ "TEMP";
	else
		szTemp = szBaseDirectory ^ "Clients" ^ PRODUCT_SUBDIR;
	endif;


	if !UpdateServicePack(szTemp, szBaseDirectory ^ PRODUCT_SUBDIR ^ "BIN") then
		goto ZFAILURE;
	endif;


	SRCDIR = WINDIR ^ "TEMP";
        //copy factor.mdb and factor.ldb to BIN and WINDOWS\SYSTEM directory
        szDBFrom = "\"" + SRCDIR + "\"";
        TARGETDIR = szBaseDirectory ^ PRODUCT_SUBDIR ^ "BIN";
        szDBTo = "\"" + TARGETDIR + "\"";

        LOGMSG="LAUNCHING " + szBaseDirectory ^ PRODUCT_SUBDIR ^ "BIN\\CPYLOCAL.EXE TO COPY FACTOR.MDB and FACTOR.LDB ";
        LOGWRITE(LOGMSG);
        LOGMSG="From " + SRCDIR + " To " + TARGETDIR + ", Force Installation=" + szForceInstall;
        LOGWRITE(LOGMSG);

        LaunchAppAndWait(szBaseDirectory ^ PRODUCT_SUBDIR ^ "BIN" ^ "cpylocal.exe", szForceInstall +
                szDBFrom + " " + szDBTo, WAIT);

        SetStatusWindow(98, "Copying Local Database...");

        TARGETDIR = WINSYSDIR;
        szDBTo = "\"" + TARGETDIR + "\"";

        LOGMSG="LAUNCHING " + szBaseDirectory ^ PRODUCT_SUBDIR ^ "BIN\\CPYLOCAL.EXE TO COPY FACTOR.MDB and FACTOR.LDB ";
        LOGWRITE(LOGMSG);
        LOGMSG="From " + SRCDIR + " To " + TARGETDIR + ", Force Installation=" + szForceInstall;
        LOGWRITE(LOGMSG);

        LaunchAppAndWait(szBaseDirectory ^ PRODUCT_SUBDIR ^ "BIN" ^ "cpylocal.exe", szForceInstall +
                szDBFrom + " " + szDBTo, WAIT);

        SetStatusWindow(99, "Copying Local Database...");

        // delete the local database from xxclient directory
        TARGETDIR = SRCDIR;
        LOGMSG="START DELETE FILE: FACTOR.MDB and FACTOR.LDB IN " + TARGETDIR;
        LOGWRITE(LOGMSG);
        DeleteFile("FACTOR.MDB");
  
        LOGMSG="END DELETE FILE: FACTOR.MDB and FACTOR.LDB IN " + TARGETDIR;
        LOGWRITE(LOGMSG);

	StatusUpdate(OFF, 0);

	// Run the client installation?
        if (bRunStandAlone) then
                
		TARGETDIR = WINDIR ^ "TEMP";

                // remove the client z files because stand-alone installation is selected
                LOGMSG="RUN STAND-ALONE, DELETE Z FILES IN " + TARGETDIR;
                LOGWRITE(LOGMSG);

                DeleteFile("*.z");
        endif;

        SetStatusWindow(99, "Finishing " + PRODUCT_NAME + " installation...");

        // add registry information
	LOGMSG="ADDING REGISTRY INFORMATION";
	LOGWRITE(LOGMSG);

        if (bRunStandAlone) then
                RegDBSetKeyValueEx(KEY_ROOT, KEY_CLIENT_PATH, REGDB_STRING, szBaseDirectory ^ PRODUCT_SUBDIR, -1);
                RegDBSetKeyValueEx(KEY_ROOT, KEY_SERVER_PATH, REGDB_STRING, "", -1);
        else
                RegDBSetKeyValueEx(KEY_ROOT, KEY_SERVER_PATH, REGDB_STRING, szBaseDirectory ^ PRODUCT_SUBDIR, -1);
        endif;

        RegDBSetKeyValueEx(KEY_ROOT ^ PRODUCT_NAME, KEY_REMOTE, REGDB_NUMBER, "0", -1);

        SetStatusWindow(100, "Finishing " + PRODUCT_NAME + " installation...");

        Delay( 1 );
	Disable(STATUS);
	Disable(FEEDBACK_FULL);

        // display finished dialog
        LOGMSG="INSTALLATION COMPLETE";
        LOGWRITE(LOGMSG);

        if (bRunStandAlone) then
                SdFinish(WELCOME_TITLE + " Complete",
                         WELCOME_TITLE + " has finished copying files to your computer.",
                         "Click Finish to complete " + WELCOME_TITLE,
                         "", "", bTemp1, bTemp2);
        else
                SdFinish(WELCOME_TITLE + " Complete",
                    WELCOME_TITLE + " has finished copying files to your computer.\n\n" +
                    "You may now run the " + PRODUCT_NAME + " Client Installation " +
                    "program on any PC requiring access to the " + PRODUCT_NAME + ".",
                    "Click Finish to complete Setup", "", "", bTemp1, bTemp2);
        endif;

	exit;

ZFAILURE:
        LOGMSG="INSTALLATION FAILED";
        LOGWRITE(LOGMSG);

	MessageBox("Installation incomplete.  Close any running\n" +
			   "applications before intalling " + PRODUCT_NAME + " Server Installation.",
			   INFORMATION);
        abort;
	
/*--------------- END OF PROGRAM --------------*\
	
		
/*---------------------------------------------*\
*
*	CheckDiskSpace
*
*	Checks the available disk space against the
*	amount requested and returns TRUE if the
*	requested space is available
*
\*---------------------------------------------*/
	
function CheckDiskSpace(nBytesRequired)

    number nActualSize;
	number nSpaceAvailable;
	number nReturn;
	string szNumber;
	string szMessage;
	BOOL bSpaceOK;
		    
begin
	
    nSpaceAvailable = GetDiskSpace(szBaseDirectory);
    
	if (nSpaceAvailable < nBytesRequired) then
		NumToStr(szNumber, nBytesRequired / 1024);
		szMessage = szNumber + " Kbytes of disk space is required for this installation.\nThe targe drive has ";
		NumToStr(szNumber, nSpaceAvailable / 1024);
		szMessage = szMessage + szNumber + " Kbytes available.\n\nContinuing may result in an incomplete installation.\n\n";
		szMessage = szMessage + "Do you still want to install to \"" + szBaseDirectory + "\"?";
		
		nReturn = MessageBox(szMessage, MB_ICONEXCLAMATION + MB_TASKMODAL + MB_YESNO + MB_DEFBUTTON2);
		
		if (nReturn = IDYES) then
			bSpaceOK = TRUE;
		else
			bSpaceOK = FALSE;
		endif;
    else
    	bSpaceOK = TRUE;
    endif;
    
    return bSpaceOK;

end;


// write log function
function LOGWRITE(logline)

begin
	logline=logline + "\n";
	WriteLine( nvFileOut, logline);
	return;
end;


// MAKE SURE szClientPath = ?:\FACTOR\CLIENTS\EXECTRAK, ? is Drive Letter.
//           szBin = ?:\FACTOR\EXECTRAK\BIN, ? is Drive Letter.
function UpdateServicePack(szClientPath, szBin)

	STRING svFileName, szTemp, szMsg;
	NUMBER nResult;
	LIST lstServiceZFiles;
begin

	szMsg = "Service Pack Update Failed.";

	//scan SERV*.Z files in ?:\FACTOR\CLIENTS\EXECTRAK directory
        lstServiceZFiles = ListCreate (STRINGLIST);
        
        if (lstServiceZFiles = LIST_NULL) then
                MessageBox ("Unable to create necessary Lists 'lstServiceZFiles'!", SEVERE);
                MessageBox (szMsg, SEVERE);
                return FALSE;
        endif;
        
        nResult = FindAllFiles(szClientPath, "SERV*.Z", svFileName, RESET);
        
        if (nResult < 0) then
		if DEVELOP then
                	MessageBox ("'SERV*.Z' file not found in '" + szClientPath + "'.", INFORMATION);
		endif;
		ListDestroy(lstServiceZFiles);
                return TRUE;
        endif;
        
        while (nResult = 0)
        	ParsePath(szTemp, svFileName, FILENAME);
		StrToUpper(szTemp, szTemp);
                if (ListAddString(lstServiceZFiles, szTemp, AFTER) < 0) then
                        MessageBox("ListAddString failed - 'lstServiceZFiles'.", SEVERE);
	                MessageBox (szMsg, SEVERE);
			ListDestroy(lstServiceZFiles);
                        return FALSE;
                endif;
                nResult = FindAllFiles(szClientPath, "SERV*.Z", svFileName, CONTINUE);
        endwhile;

	VarSave(SRCTARGETDIR);

	// Decompress Service Pack
	if !DecompressServicePack(szClientPath, lstServiceZFiles) then
                MessageBox (szMsg, SEVERE);
		DeleteServiceZDirectory(lstServiceZFiles);
		VarRestore(SRCTARGETDIR);
		ListDestroy(lstServiceZFiles);
		return FALSE;
	endif;

	VarRestore(SRCTARGETDIR);

	if !CopyServicePackFile(lstServiceZFiles, szBin) then
                MessageBox (szMsg, SEVERE);
		DeleteServiceZDirectory(lstServiceZFiles);
		VarRestore(SRCTARGETDIR);
		ListDestroy(lstServiceZFiles);
		return FALSE;
	endif;

	DeleteServiceZDirectory(lstServiceZFiles);
	VarRestore(SRCTARGETDIR);
	ListDestroy(lstServiceZFiles);
	return TRUE;

end;


// Decompress Service Pack Z files to WINDOWS\TEMP\SERV????.Z directory
function DecompressServicePack(szSrcPath, lstName)

	STRING szName;
	NUMBER nReturn;
begin

	//decompress SERV???.Z
	
        nReturn = ListSetIndex (lstName, LISTFIRST);

        while (nReturn != END_OF_LIST)
                ListCurrentString(lstName, szName);

		if !DecompressZfile(szSrcPath, szName, COMP_NORMAL) then
			return FALSE;
		endif;

                nReturn = ListSetIndex (lstName, LISTNEXT);
        endwhile;

	return TRUE;

end;


function DeleteServiceZDirectory(lstName)

	STRING szName, szTemp;
begin
        TARGETDIR = WINDIR ^ "TEMP";
        SRCDIR = TARGETDIR;

        nReturn = ListSetIndex (lstName, LISTFIRST);

        while ((nReturn != END_OF_LIST))
                ListCurrentString(lstName, szName);

	        szTemp = WINDIR ^ "TEMP" ^ szName + "DIR";
	        DeleteDir(szTemp, ALLCONTENTS);
                nReturn = ListSetIndex (lstName, LISTNEXT);
        endwhile;
end;


function CopyServicePackFile(lstZDir, szBin)

	STRING szPath, szName, svFileName, szDest, szDir;
	NUMBER nReturn, nResult, nvPosi;
	LIST lstFileList;
	BOOL bSharedFile, bSelfRegister;

begin

	StrToUpper(szBin, szBin);
	nvPosi = StrFind(szBin, "BIN");

	if nvPosi < 0 then
        	MessageBox ("'" + szBin + "' directory is not valid. MUST BE '?:\\FACTOR\\EXECTRAK\\BIN'.", SEVERE);
	else
		StrSub(szDir, szBin, 0, nvPosi - 1);
	endif;

        nReturn = ListSetIndex (lstZDir, LISTFIRST);

        while ((nReturn != END_OF_LIST))
                ListCurrentString(lstZDir, szName);
		szPath = WINDIR ^ "TEMP" ^ szName + "DIR";
		
		//scan *.* files in C:\WINDOWS\TEMP\SERV????.Z directory
	        nResult = FindAllFiles(szPath, "*.*", svFileName, RESET);
	        
	        if (nResult < 0) then
			if DEVELOP then
	                	MessageBox ("'*.*' file not found in '" + szPath + "'.", SEVERE);
			endif;
		else
			if szName = "SERVCTL.Z" || szName = "SERVDLL.Z" || szName = "SERVOLE.Z"
			   || szName = "SERVRTM.Z" || szName = "SERVSHAR.Z" then
				bSelfRegister = TRUE;
			else
				bSelfRegister = FALSE;
			endif;

			bSharedFile = FALSE;
	
			if szName = "SERVFACT.Z" then
				szDest = szDir;  // ?:\FACTOR\EXECTRAK
			elseif szName = "SERVBIN.Z" then
				szDest = szBin;  // ?:\FACTOR\EXECTRAK\BIN
			elseif szName = "SERVDLL.Z" then
				szDest = szDir ^ "DLL";  // ?:\FACTOR\EXECTRAK\DLL
			elseif szName = "SERVOLE.Z" then
				szDest = szDir ^ "OLE";  // ?:\FACTOR\EXECTRAK\OLE
			elseif szName = "SERVDB.Z" then
				szDest = WINDIR ^ "TEMP";
			elseif szName = "SERVSHAR.Z" then
	        			szDest = WINSYSDISK ^ "Program Files" ^ "Common Files" ^ 
					"Microsoft Shared" ^ "DAO";
			else
				szDest = WINSYSDIR;
				bSharedFile = TRUE;
			endif;

			ListDestroy(lstFileList);
		        lstFileList = ListCreate (STRINGLIST);
		        
		        if (lstFileList = LIST_NULL) then
		                MessageBox ("Unable to create necessary Lists 'lstFileList'!", SEVERE);
		                return FALSE;
		        endif;

		        while (nResult = 0)
		        	ParsePath(szTemp, svFileName, FILENAME);
				StrToUpper(szTemp, szTemp);
		                if (ListAddString(lstFileList, szTemp, AFTER) < 0) then
		                        MessageBox("ListAddString failed - 'lstFileList'.", SEVERE);
					ListDestroy(lstFileList);
		                        return FALSE;
		                endif;
		                nResult = FindAllFiles(szPath, "*.*", svFileName, CONTINUE);
		        endwhile;

			CheckCopyFiles(lstFileList, szPath, szDest, bSharedFile, bSelfRegister);
	        endif;

                nReturn = ListSetIndex (lstZDir, LISTNEXT);
        endwhile;

	ListDestroy(lstFileList);

	return TRUE;

end;


function CheckCopyFiles(lstFileList, szFrom, szDest, bSharedFile, bSelfRegister)

	STRING szName, szTemp, szVersionFrom, szVersionTo, szDateFrom, szDateTo;
	NUMBER nReturn, nvXCopyFileParm, nvPosi, nvNotApplicable;
	BOOL bGoCopy;
begin

	SRCDIR = szFrom;
	TARGETDIR = szDest;

        nReturn = ListSetIndex (lstFileList, LISTFIRST);

        while ((nReturn != END_OF_LIST))
                ListCurrentString(lstFileList, szName);

		
		if !bSelfRegister then
			nvPosi = StrFind(szName, ".MDB");
			if nvPosi <0 then
				nvPosi = StrFind(szName, ".QRT");
			endif;
			if nvPosi >=0 then
				bGoCopy = TRUE;
			else // EXE file
				if fnFindFile(szDest, szName) then
					szTemp = szFrom ^ szName;
					if VerGetFileVersion(szTemp, szVersionFrom) != 0 then
						szVersionFrom = "";
					endif;

					GetFileInfo (szTemp, FILE_DATE, nvNotApplicable, szDateFrom);

					szTemp = szDest ^ szName;
					if VerGetFileVersion(szTemp, szVersionTo) != 0 then
						szVersionTo = "";
					endif;

					GetFileInfo (szTemp, FILE_DATE, nvNotApplicable, szDateTo);

					if DEVELOP then
						MessageBox(szFrom ^ szName + ":\nszVersionFrom=" + szVersionFrom +
							szVersionFrom + ":\nszVersionTo=" + szVersionTo, INFORMATION);
					endif;

					if szVersionFrom < szVersionTo then
						bGoCopy = FALSE;
					else
						if szVersionFrom = szVersionTo then
							// check the date
							if szDateFrom > szDateTo then
								bGoCopy = TRUE;
							else
								bGoCopy = FALSE;
							endif;
						else
							bGoCopy = TRUE;
						endif;
					endif;
				else
					bGoCopy = FALSE;
				endif;
			endif;
		else
			bGoCopy = TRUE;
		endif;

		if DEVELOP then
			if bGoCopy then
				MessageBox("File=" + szName + "\nbGoCopy=TRUE", INFORMATION);
			else
				MessageBox("File=" + szName + "\nbGoCopy=FALSE", INFORMATION);
			endif;
		endif;

		if bGoCopy then
			nvXCopyFileParm = COMP_NORMAL;

			if bSharedFile then
				nvXCopyFileParm = nvXCopyFileParm | SHAREDFILE;
			endif;

			if bSelfRegister then
				nvXCopyFileParm = nvXCopyFileParm | SELFREGISTER;
			endif;

			XCopyFile(szName, szName, nvXCopyFileParm);

			//launch and register some files
			
			if szName = "PPLINE32.EXE" then
		        	LaunchAppAndWait(szDest ^ "PPLINE32.EXE", "", WAIT);
			endif;
			if szName = "TCLCOM32.EXE" then
		        	LaunchAppAndWait(szDest ^ "TCLCOM32.EXE", "", WAIT);
			endif;
			if szName = "FACTOLE.EXE" then
		        	LaunchAppAndWait(szDest ^ "FACTOLE.EXE", "", WAIT);
			endif;
		endif;

                nReturn = ListSetIndex (lstFileList, LISTNEXT);
        endwhile;

	return TRUE;

end;


function DecompressZfile(szPath, szID, nvDecompressParm)

	NUMBER nReturn;
begin

	TARGETDIR = WINDIR ^ "TEMP" ^ szID + "DIR";
	
	SRCDIR = szPath;

	// ************************* if command line =-a then FORCED INSTALLATION of ALL FILES
        if (CMDLINE = "-A" || CMDLINE = "-a") then
                LOGMSG="START FORCED COPY OF " + szID + " FILES IN " + TARGETDIR;
                LOGWRITE(LOGMSG);
        else
		// ************************* Normal install
                LOGMSG="START NORMAL COPY OF factbin FILES IN " + TARGETDIR;
                LOGWRITE(LOGMSG);
        endif;

	nReturn = CompressGet(szID, "*.*", nvDecompressParm);

        if nReturn >= 0 then
	        if (CMDLINE = "-A" || CMDLINE = "-a") then
	                LOGMSG="END FORCED COPY OF " + szID + " FILES IN " + TARGETDIR;
	                LOGWRITE(LOGMSG);
	        else
	                LOGMSG="END NORMAL COPY OF " + szID + " FILES IN " + TARGETDIR;
	                LOGWRITE(LOGMSG);
		endif;
        endif;

        if (nReturn != FS_DONE) then
                LOGMSG="ERRORFILENAME=" + ERRORFILENAME;
                LOGWRITE(LOGMSG);
        endif;

        switch(nReturn)
                case FS_CREATEDIR:
	                LOGMSG="Failed to Create target directory.";
	                LOGWRITE(LOGMSG);
                        MessageBox(LOGMSG, SEVERE);
                case FS_PACKAGING:
	                LOGMSG="Unable to find file in package list.";
	                LOGWRITE(LOGMSG);
                        MessageBox(LOGMSG, SEVERE);
                case FS_FILENOTINLIB:
	                LOGMSG="Unable to find target file in compressed lib.";
	                LOGWRITE(LOGMSG);
                        MessageBox(LOGMSG, SEVERE);
                case FS_DONE:
                        // do nothing
                default:
	                LOGMSG="General file copy failure.";
	                LOGWRITE(LOGMSG);
                        MessageBox(LOGMSG, SEVERE);
        endswitch;

        if nReturn < 0 then
		return FALSE;
	else
		return TRUE;
	endif;
end;


function fnFindFile(szPath, szFile)

        STRING svResult;

begin

        svResult = "";

        FindFile(szPath, szFile, svResult);

        if (svResult = "") then
                return FALSE;
        else
                return TRUE;
        endif;

end;


function BaseInstalled()

	STRING szTemp;

begin

	// search for factmenu.exe
        if (FindFile(szBaseDirectory ^ PRODUCT_SUBDIR, PRODUCT_KEY, szTemp) >= 0) then
                return TRUE;
        else
                return FALSE;
        endif;


end;


#include "sddialog.rul"
